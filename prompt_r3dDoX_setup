function prompt_r3dDoX_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
      git-info
  fi
}

function prompt_r3dDoX_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_r3dDoX_precmd

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:dirty' format "%F{yellow}*>%f"
  zstyle ':prezto:module:git:info:clean' format "%F{yellow}>%f"
  zstyle ':prezto:module:git:info:branch' format "%F{yellow}<%b%f"
  zstyle ':prezto:module:git:info:commit' format "%F{yellow}<%c%f"
  zstyle ':prezto:module:git:info:ahead' format '%F{yellow}+%A%f'
  zstyle ':prezto:module:git:info:behind' format '%F{yellow}-%B%f'
  zstyle ':prezto:module:git:info:unmerged' format '%F{red}%U%f'
  zstyle ':prezto:module:git:info:keys' format \
    'ref' '$(coalesce "%b" "%c")%C' \
    'status' '%s%D%A%B%S%a%d%m%r%U%u'

  # Define prompts.
  PROMPT='╭─%F{green}%n@%m%f %F{cyan}%~%f ${(e)git_info[ref]}${(e)git_info[status]}
╰─%# '
  RPROMPT='%(?..%F{red}%? ↵%f)'
}

prompt_r3dDoX_setup "$@"
